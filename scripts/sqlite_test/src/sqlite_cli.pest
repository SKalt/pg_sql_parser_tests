program = {SOI ~ newline* ~ (statement ~ newline*)* ~ newline* ~ EOI}

statement = {
    cli_comment
  | dot_command
  | sql_statement
}

sql_statement = {
  (sql_comment* ~ sql+ ~ (";" ~ sql_comment?)?)
  | ";"
}

sql = {
    c_comment
  | sql_comment
  | parens
  | op
  | word
  | string
  | newline
  | bracket_quote
}

string = {single_quote_string | double_quote_string}
single_quote_string = {"'" ~ (!"'" ~ ANY)* ~ "'"}
double_quote_string = {"\"" ~ (!"\"" ~ ANY)* ~ "\""}
semicolon = @{";" ~ (" " | "\t")* ~ sql_comment?}

cli_comment =  {"#"  ~ not_newline* ~ newline*}
dot_command = @{"."  ~ not_newline* ~ newline*}
sql_comment =  {"--" ~ not_newline* ~ newline*}
c_comment =    {"/*" ~(!"*/" ~ ANY)* ~ "*/" }
bracket_quote = { "[" ~ (!"]" ~ ANY)* ~ "]" }
word = {(alnum | "." | "_")+}

numeric = _{('0'..'9')+}
alpha = _{('a'..'z' | 'A'..'Z')+}
alnum = _{alpha | numeric}
parens = {"(" ~ sql* ~ ")"}

op = {"!" | "$" | "%" | "&" | '*'..'/' | ":" | '<'..'@' | '{'..'~' }

newline = @{"\r"? ~ "\n"}
not_newline = _{!newline ~ ANY}

WHITESPACE = _{(" " | "\t")+}
